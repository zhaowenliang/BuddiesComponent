/**
 * Maven Publish Helper
 *
 * Requires Android Gradle plugin 3.6.0 or higher (available since Android Studio 3.6).
 * See also: https://developer.android.com/studio/build/maven-publish-plugin
 *
 * @Author Robert PÃ¶sel
 * @Version 1.5* @Date 3.3.2020
 */

apply plugin: 'maven-publish'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // Adds javadocs and sources as separate jars.
                artifact androidSourcesJar

                // You can customize attributes of the publication here or in module's build.gradle file.
                groupId = rootProject.aliyunMaven.publishGroup
                // artifactId = 'custom-artifact'
                version = android.defaultConfig.versionName // or just '1.0'
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = rootProject.aliyunMaven.releaseRepository
                def snapshotsRepoUrl = rootProject.aliyunMaven.snapshotsRepository
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username rootProject.aliyunMaven.mavenUser
                    password rootProject.aliyunMaven.mavenPass
                }
            }
        }
    }
}
